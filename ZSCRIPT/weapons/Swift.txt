Class Swift : Weapon
{
	Default
	{
		Weapon.slotNumber 4;
	}
	
	Action bool A_CheckRageAmmo() 
	{
		if (Gardevoir(self).RageValue <= 0) return false;
		return true;
	}
	
	override void DoEffect()
	{
		players[consoleplayer].WeaponState |= WF_WEAPONBOBBING;
		super.DoEffect();
	}
	
	states 
	{
		Select :
			SWFT A 1 A_Raise();
			Loop;
		Deselect :
			SWFT A 1 A_Lower();
			Loop;
		Ready :
			SWFT ABCD 3 A_WeaponReady();
			Loop;
		Fire :
			TNT1 A 0 
			{
				int count = Min(50, 10 * Gardevoir(self).getRageModifier());
				
				A_StartSound("attack/SWFTATTK", CHAN_WEAPON);
				A_FireProjectile("SwiftProjectile");
				while (count >= 1) {
					A_FireProjectile("SwiftProjectileSeeker", random(-10, 10), false, random(-10, 10), random(-10, 10), 0, random(-10, 10));
					--count;
				}
				
				Gardevoir(self).subtractAnger(10);
				
			}
			SWFT FGHIJKL 2 A_SetTics(Gardevoir(self).GetTicModifier(2));
			TNT1 A 0 A_Refire();
			Goto Ready;
	}
}

Class SwiftProjectile : Actor
{
	
	default
	{
		Radius 10;
		Height 10;
		Speed 40;
		Projectile;
		Scale 0.75;
		+NoExtremeDeath;
		+Randomize;
		+ScreenSeeker;
		DeathSound "attack/SWFTDEAT";
	}
	
	override void BeginPlay()
	{
		super.BeginPlay();
		
		Self.setDamage(max(2, 30 * Gardevoir(players[consoleplayer].mo).getRageModifier()));
	}
	
	States 
	{
		Spawn:
			SWFP ABCDEFGH 2 Bright
			{
				A_Weave(1, 1, 2, 2);
			}
			Loop;
		Death:
			SWFP A 1 {
				A_SpawnItemEx("SwiftDrop", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360));
				A_SpawnItemEx("SwiftDrop", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360));
				A_SpawnItemEx("SwiftDrop", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360));
			}
			stop;
	}

}

Class SwiftProjectileSeeker : SwiftProjectile
{
	
	States 
	{
		Spawn:
			SWFP ABCDEFGH 2 Bright
			{
				A_Weave(1, 1, 2, 2);
				A_SeekerMissile(0, 15, SMF_LOOK | SMF_PRECISE, 256, 10);
			}
			Loop;
	}

}

Class SwiftDrop : Actor
{
	Default
	{
		Damage 0;
		Speed 5;
		-NoGravity;
		Scale 0.5;
	}
	States 
	{
		Spawn:
			SWFD ABCDABCDABCD 1 Bright;
		Death:
			Stop;
	}
}