class GHAGKeyEvents : EventHandler
{   
	override bool InputProcess (InputEvent e)
	{
		if (e.Type == InputEvent.Type_KeyDown)
			SendNetworkEvent("KinesisActivate", e.KeyScan); 
		if (e.Type == InputEvent.Type_KeyUp)
			SendNetworkEvent("KinesisDeactivate", e.KeyScan); 
		return false;
   }

   override void NetworkProcess(ConsoleEvent e)
   {               
		if (e.Name == "KinesisActivate")  
		{
			let gardie = Gardevoir(players[consoleplayer].mo);
		
			int key1, key2;
			[key1, key2] = Bindings.GetKeysForCommand("kinesis");
			if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
			{
				FLineTraceData shot;
				
				//Prioritize walls and switches first
				gardie.LineTrace(gardie.angle, 2048, gardie.pitch, TRF_THRUACTORS | TRF_THRUHITSCAN | TRF_THRUBLOCK | TRF_NOSKY, gardie.height - 12, 0, 0, shot);
				if (shot.HitLine)
				{
					if (shot.hitline.special != 243 && shot.hitline.special != 244)
					{
						int orgSpeed = shot.HitLine.args[1];
						if (shot.HitLine.args[0] == 0 && shot.HitLine.args[1] != 0) shot.HitLine.args[1] += int(shot.HitLine.args[1] * gardie.getRageModifier() * 2);
						bool activate = shot.HitLine.Activate(Gardie, shot.LineSide, SPAC_Use | SPAC_Impact);
						if (activate)
						{
							KinesisPing.spawn('KinesisPing', shot.HitLocation);
							return;
						} else {
							shot.HitLine.args[1] = orgSpeed;
						}
					}
				}
				
				//Perform item pulls and barrel tossing
				int kinesis_distance = 32;
				Array<Actor> list;
				int iterations = 0;
				while (kinesis_distance < 2048)
				{
					gardie.LineTrace(gardie.angle, kinesis_distance, gardie.pitch, TRF_THRUACTORS | TRF_THRUHITSCAN, gardie.height - 12, 0, 0, shot);
					
					let blast = KinesisBlast(Actor.spawn('KinesisBlast', shot.HitLocation));
					blast.getRadialActors();
					list.copy(blast.list);
					
					if (list.size() != 0)
					{
						blast.startpulling = true;
						break;
					}
					
					kinesis_distance += 32;
					++iterations;
				}
			}
		}
	}
}

Class HudModifier : Inventory
{}

Class KinesisBlast : Actor
{

	Array<Actor> list;
	bool startPulling;
	bool slapProjectile;

	void getRadialActors()
	{
		list.clear();
		
		int radial = 64;
		
		let gardie = Gardevoir(players[consoleplayer].mo);
		ThinkerIterator it = ThinkerIterator.Create("Actor");
		Actor mo;
		while ( (mo = Actor(it.Next ())) )
		{
			if (!Inventory(mo) && !ExplosiveBarrel(mo)) continue;
			
			if (Inventory(mo)) {
				if (Inventory(mo) && Inventory(mo).owner is "PlayerPawn") continue;
			}
			
			if (Gardie.distance2D(mo) <= 64) continue; //ignore items within single step distance
			
			if (distance3D(mo) <= radial && isVisible(mo, true)) {
			
				if (radial == 64) 
				{	
					it.Reinit();
					list.clear();
					radial = 256;
				}

				list.push(mo);
				KinesisPing(Actor.spawn('KinesisPing', mo.pos));
			}
		}
	}
	
	void ReturnToSender()
	{
		let gardie = Gardevoir(players[consoleplayer].mo);
		
		for (int i = 0; i < list.size(); ++i)
		{
			if (!list[i]) {
				list.delete(i--);
				list.shrinkToFit();
				continue;
			}
			else
			{
				Actor victim = list[i];
				
				if (ExplosiveBarrel(victim))
				{
					if (!victim.InStateSequence(victim.CurState, victim.ResolveState("Death")))
					{
						if (!victim.tracer)
						{
							victim.target = gardie;
							victim.bMissile = true;
							victim.bNoExplodeFloor = true;
							victim.bSeekerMissile = true;
							victim.bRollSprite = true;
							victim.bRollCenter = true;
							for (int i = 0; i < 10; ++ i) victim.A_SeekerMissile(360, 90, SMF_LOOK | SMF_PRECISE);
							KinesisPing(Actor.spawn('KinesisPing', victim.pos));
							if (victim.tracer)
							{
								victim.A_SeekerMissile(360, 90, SMF_LOOK | SMF_PRECISE);
								victim.A_SetRoll(victim.roll + 25, SPF_INTERPOLATE);
							} 
							else
							{
								victim.bMissile = false;
								victim.damageMobj(victim, victim, 9999999, '');
								list.delete(i--);
								list.shrinkToFit();
							}
						}
						else
						{
							victim.Vel3DFromAngle(40, victim.angleTo(victim.tracer), 0);
							victim.A_SetRoll(victim.roll + 25, SPF_INTERPOLATE);
						}
					}
					else
					{
						list.delete(i--);
						list.shrinkToFit();
					}
					continue;
				}
				
				if (Inventory(victim))
				{
					Inventory(victim).bNoCLip = true;
				}
	
				if (gametic % 2 == 0)
				{
					double angle = gardie.AngleTo(victim);
					Vector2 move = gardie.AngleToVector(angle, -32);
					victim.Vel.XY = move;
					victim.Vel.Z = 64. / victim.Mass;
					
					if (gardie.distance2D(victim) <= 64)
					{
						if (Inventory(victim))
						{
							if (RageEssence(Victim))
							{
								int rage = gardie.GetRageValue();
								if (rage < gardie.rageconstant) Inventory(victim).touch(gardie);
							} 
							else 
							{
								if (Inventory(victim) && !victim.bDestroyed) 
								{
									gardie.touch(victim);
								}
							}
							if (victim) {
								victim.bNoClip = false;
								victim.vel = (0, 0, 0);
							}
						}
						list.delete(i);
						list.ShrinkToFit();
					}
				}
			}
		}
	}
	
	default
	{
		+NoGravity;
	}
	
	states
	{
		Spawn:
			TNT1 A 1 Bright
			{
				if (invoker.startpulling) self.returnToSender();
				if (invoker.list.size() == 0) self.destroy();
			}
			loop;
	}
}

Class KinesisPing : Actor
{
	default
	{
		+NoGravity;
	}
	States
	{
		Spawn :
			TNT1 A 4 A_SetTics(Random(1, 3));
			TNT1 A 0 A_StartSound("psychic/Ping");
			PING ABCDEFGHIJKLMN 1 Bright;
			Stop;
	}
}