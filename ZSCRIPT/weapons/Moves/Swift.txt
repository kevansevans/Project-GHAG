Class Swift : Weapon
{
	Default
	{
		Weapon.slotNumber 4;
	}
	
	Action bool A_CheckRageAmmo() 
	{
		if (Gardevoir(self).RageValue <= 0) return false;
		return true;
	}
	
	states 
	{
		Select :
			TNT1 A 0 Fast 
			{
				if (!A_CheckRageAmmo()) return resolveState("Select.NoAmmo");
				return resolveState(null);
			}
			SWFT A 1 A_Raise();
			Loop;
		Select.NoAmmo  :
			SWFT E 1 A_Raise();
			Loop;
		Deselect :
			TNT1 A 0
			{
				if (!A_CheckRageAmmo()) return resolveState("Deselect.NoAmmo");
				return resolveState(null);
			}
			SWFT A 1 A_Lower();
			Loop;
		Deselect.NoAmmo  :
			SWFT E 1 A_Lower();
			Loop;
		Ready :
			TNT1 A 0
			{
				if (!A_CheckRageAmmo()) return resolveState("Ready.NoAmmo");
				return resolveState(null);
			}
			SWFT ABCD 3 A_WeaponReady();
			Loop;
		Ready.NoAmmo :
			SWFT E 1 A_WeaponReady();
			TNT1 A 0
			{
				if (!A_CheckRageAmmo()) return resolveState("Ready.NoAmmo");
				else return resolveState("Ready");
			}
			Loop;
		Fire :
			TNT1 A 0
			{
				if (!A_CheckRageAmmo()) return resolveState("Ready.NoAmmo");
				return resolveState(null);
			}
			TNT1 A 0 
			{
				Gardevoir(self).RageValue -= 1;
				int count = Gardevoir(self).RageValue / 200;
				count = min(10, count);
				A_StartSound("attack/SWFTATTK", CHAN_WEAPON);
				while (count >= 0) {
					A_FireCustomMissile("SwiftProjectile", random(-3, 3), false, random(-3, 3), random(-3, 3));
					--count;
				}
			}
			SWFT FGHIJKL 2 {
				A_SetTics(Gardevoir(self).GetTicModifier(2));
				if (Gardevoir(self).RageValue <= 0) {
					Gardevoir(self).RageValue = 0;
					return resolveState("Ready.NoAmmo");
				}
				return resolveState(null);
			}
			TNT1 A 0 A_Refire();
			Goto Ready;
	}
}

Class SwiftProjectile : Actor
{
	
	default
	{
		Radius 10;
		Height 10;
		Speed 20;
		Damage 40;
		Projectile;
		Scale 0.75;
		+NoExtremeDeath;
		+Randomize;
		+ScreenSeeker;
		DeathSound "attack/SWFTDEAT";
	}
	
	States 
	{
		Spawn:
			SWFP ABCDEFGH 2 Bright
			{
				A_Weave(1, 1, 2, 2);
				A_SeekerMissile(0, 15, SMF_LOOK | SMF_PRECISE, 256, 256);
			}
			Loop;
		Death:
			SWFP A 1 {
				A_SpawnItemEx("SwiftDrop", 0, 0, 0, random(3, 5), random(3, 5), random(3, 5), random(0, 360));
				A_SpawnItemEx("SwiftDrop", 0, 0, 0, random(3, 5), random(3, 5), random(3, 5), random(0, 360));
				A_SpawnItemEx("SwiftDrop", 0, 0, 0, random(3, 5), random(3, 5), random(3, 5), random(0, 360));
			}
			stop;
	}

}

Class SwiftDrop : Actor
{
	Default
	{
		Damage 0;
		Speed 5;
		-NoGravity;
		Scale 0.5;
	}
	States 
	{
		Spawn:
			SWFD ABCDABCDABCD 1 Bright;
		Death:
			Stop;
	}
}