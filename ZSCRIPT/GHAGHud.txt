Class GHAGStatus : BaseStatusBar
{

	double v_width;
	double v_height;
	vector2 scale;
	
	bool lowHealth;
	
	string wtfString;
	
	override void Init() {
		Super.Init();
		
		scale = getHUDScale();
		
		SetSize(0, Screen.getWidth() / (scale.x / 2), Screen.getHeight() / (scale.y / 2));
		
		wtfString = wtf();
	}
	
	override void Draw(int state, double tic) {
		
		Super.Draw(state, tic);
		
		BeginHUD();
		
		scale = getHUDScale();
		
		if (scale.x == 0) return;
		
		v_width = Screen.getWidth() / (scale.x);
		v_height = Screen.getHeight() / (scale.y);
		
		let gardie = Gardevoir(CPlayer.mo);
		if (!gardie) return;
		
		Font fnt = "Pokemon";
		HUDFont pokefont = HUDFont.Create(fnt);
		
		float xy_scale = 1.5;
		
		if (Gardie.health >= 175) {
			DrawBar("hpbar", "hparmbg", Min(Gardie.health, 350), 350, (86, v_height - 103), 0, 0, DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP);
			if (gardie.health > 350) {
				DrawBar("armbar", "hparmbg", Gardie.health - 350, 500 - 350, (86, v_height - 103), 0, 0, DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP);
			}
			lowHealth = false;
		} else if (gardie.health > 70) {
			DrawBar("hpbarhlf", "hparmbg", Min(Gardie.health, 350), 350, (86, v_height - 103), 0, 0, DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP);
			lowHealth = false;
		} else {
			DrawBar("angbar", "hparmbg", Min(Gardie.health, 350), 350, (86, v_height - 103), 0, 0, DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP);
			if (!lowHealth)
			{
				S_StartSound("ambient/LOWHEALTH", CHAN_BODY);
				lowHealth = true;
			}
		}
		DrawBar("armbar", "hparmbg", GetArmorAmount(), 200, (86, v_height - 82), 0, 0, DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP);
		DrawBar("angbar", "hparmbg", Gardie.RageValue, Gardie.RageConstant, (86, v_height - 19), 0, 0, DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP);
		DrawImage("BATTLHUD", (5, v_height - 110), DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP);
		
		DrawString(pokefont, Gardie.player.ReadyWeapon.GetTag(), (30, v_height - 140),  DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, Font.CR_UNTRANSLATED, 1, -1, 4, (xy_scale, xy_scale));
		if (Gardie.player.ReadyWeapon.Ammo1) {
			DrawString(pokefont, 
						"Ammo: " .. Gardie.player.ReadyWeapon.Ammo1.amount .. "/" .. Gardie.player.ReadyWeapon.Ammo1.maxamount, 
						(30, v_height - 125), 
						DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 
						Font.CR_UNTRANSLATED, 1, -1, 4, 
						(xy_scale, xy_scale));
		}
		DrawString(pokefont,
					Gardie.health .. "/350 HP ",
					(120, v_height - 106), 
					DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 
					Font.CR_UNTRANSLATED, 1, -1, 4,
					(1.25, 1.25));
					
		DrawString(pokefont,
					GetArmorAmount() .. " Armor",
					(120, v_height - 82), 
					DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 
					Font.CR_UNTRANSLATED, 1, -1, 4,
					(1.25, 1.25));
			
		if (gametic % 10 == 0) wtfString = wtf();
			
		if (Gardie.OverTheEdge) {
		
			DrawString(pokefont,
					wtfString,
					(140 + random(-5, 5), v_height - 25 + random(-5, 5)), 
					DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 
					Font.CR_UNTRANSLATED, 1, -1, 4,
					(1.25, 1.25));
		
			DrawString(pokefont,
					GetAngerString(),
					(23 + random(-5, 5), v_height - 60 + random(-5, 5)), 
					DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 
					Font.CR_FIRE, 1, -1, 4, 
					(3.5, 3.5));
		}
	
		DrawString(pokefont,
					GetAngerString(),
					(23, v_height - 60), 
					DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 
					Font.CR_FIRE, 1, -1, 4, 
					(3.5, 3.5));
					
		string angy_percent = String.Format("%.1f", (Float(Gardie.RageValue) / 10));
		DrawString(pokefont,
					Gardie.RageValue > Gardie.RageConstant ? wtfString : angy_percent .. '/' .. Gardie.RageConstant / 10,
					(140, v_height - 25), 
					DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 
					Font.CR_UNTRANSLATED, 1, -1, 4,
					(1.25, 1.25));
		
		int x_tracker = v_width - 1;
		for (let inv = gardie.inv; inv; inv = inv.inv)
		{
			string wep = '';
			if (inv is "weapon") {
				let gun = Weapon(inv);
				if (gun == Gardie.player.ReadyWeapon) {
					wep = "wepactiv";
				} else {
					wep = "wepinnac";
					if (gun.Ammo1) {
						if (gun.Ammo1.amount == 0) wep = "wepempty";
					}
				}
				
				drawImage(wep, (x_tracker -= (8 * 2.5), v_height - 20), DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 1, (-1, -1), (2.5, 2.5));
			}
		}
		
		bool locks[6];
		String image;
		for (int i = 0; i < 6; i++) locks[i] = CPlayer.mo.CheckKeys(i + 1, false, true);
		// key 1
		if (locks[1] && locks[4]) image = "STKEYS6";
		else if (locks[1]) image = "STKEYS0";
		else if (locks[4]) image = "STKEYS3";
		DrawImage(image, (v_width - 20, v_height - 35),  DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 1, (-1, -1), (1.5, 1.5));
		// key 2
		if (locks[2] && locks[5]) image = "STKEYS7";
		else if (locks[2]) image = "STKEYS1";
		else if (locks[5]) image = "STKEYS4";
		else image = "";
		DrawImage(image, (v_width - 35, v_height - 35),  DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 1, (-1, -1), (1.5, 1.5));
		// key 3
		if (locks[0] && locks[3]) image = "STKEYS8";
		else if (locks[0]) image = "STKEYS2";
		else if (locks[3]) image = "STKEYS5";
		else image = "";
		DrawImage(image, (v_width - 50, v_height - 35),  DI_ITEM_LEFT_TOP | DI_SCREEN_LEFT_TOP, 1, (-1, -1), (1.5, 1.5));
		
	}
	
	string wtf()
	{
		string value = '';
		for (int i = 0; i < 10; ++i)
		{
			int rng = Random(1, 2);
			if (rng % 2 == 0) value = value .. '!';
			else value = value .. '?';
		}
		return value;
	}
	
	string GetAngerString()
	{
		let gardie = Gardevoir(CPlayer.mo);
		if (!gardie) return "I'm so angry, I simply do not exist!";
		
		int anger = Gardie.RageValue / (Gardie.RageConstant >= 1000 ? Gardie.RageConstant : 1000 / 10);
		if (anger < 0) return "your turn will come....";
		if (anger > 10) return "HELL HATH NO FURY";
		switch (anger) {
			case 0:
				return "Calm";
			Case 1:
				return "Annoyed";
			case 2:
				return "Bitter";
			case 3:
				return "Heated";
			case 4:
				return "Fuming";
			case 5:
				return "Irate";
			case 6:
				return "Savage";
			case 7:
				return "Scorn";
			case 8:
				return "Livid";
			case 9:
			case 10:
				return "MURDEROUS";
		}
		
		return "So hecking angery, that idk what to call this!";
	}
}