Class Psychic : Pokemonmove
{
	Default
	{
		Weapon.SlotNumber 4;
		
		Tag "Psychic";
		Inventory.PickupMessage "Tada! Gardevoir learned \ctPsychic!"; // and thankfully didn't forget anything!
	}
	
	States
	{
		Spawn:
			TMPS A -1;
			stop;
		Select:
			MPUN A 1 A_GHAGRaise();
			Loop;
		Deselect:
			MPUN A 1 A_GHAGLower();
			Loop;
		Ready:
			MPUN A 1 
			{
				A_WeaponReady();
				A_Overlay(-1, "Ready.NoOverlay");
			}
			Loop;
		Fire:
			TNT1 A 0 A_StartSound("attack/PsychicCharge", CHAN_AUTO);
			HAND HHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 1
			{
				double rngrange = 2;
				A_OverlayOffset(0, random(-rngrange, rngrange), 43 + random(-rngrange, rngrange), WOF_INTERPOLATE);
				A_Overlay(-1, "Fire.OverlayCharge");
				A_OverlayFlags(-1, PSPF_FLIP | PSPF_MIRROR, true);
				A_OverlayOffset(-1, -35 - random(-rngrange, rngrange), random(-rngrange, rngrange), WOF_INTERPOLATE);
			}
		Fire.Loop:
			HAND K 1
			{
				A_FireProjectile('PsychicMissile');
				double rngrange = 2;
				A_OverlayOffset(0, random(-rngrange, rngrange), 43 + random(-rngrange, rngrange), WOF_INTERPOLATE);
				A_Overlay(-1, "Fire.OverlayMirror");
				A_OverlayFlags(-1, PSPF_FLIP | PSPF_MIRROR, true);
				A_OverlayOffset(-1, -35 - random(-rngrange, rngrange), random(-rngrange, rngrange), WOF_INTERPOLATE);
				A_SetTics(Gardevoir(self).GetTicModifier(8, true));
				Gardevoir(self).subtractanger(5);
			}
			HAND K 1
			{
				double rngrange = 2;
				A_Overlay(-1, "Fire.OverlayMirror");
				A_OverlayFlags(-1, PSPF_FLIP | PSPF_MIRROR, true);
				A_OverlayOffset(-1, -35 - random(-rngrange, rngrange), random(-rngrange, rngrange), WOF_INTERPOLATE);
			}
			TNT1 A 0 A_Refire('Fire.Loop');
			Goto Ready;
		Fire.OverlayCharge:
			HAND H 1;
			stop;
		Fire.OverlayMirror:
			HAND K 1;
			loop;
		Ready.NoOverlay:
			TNT1 A 1;
			stop;
	}
}

Class PsychicMissile : Actor
{
	Default
	{
		Speed 60;
		Damage 10;
		Projectile;
		+RollSprite;
	}
	
	override void BeginPlay()
	{
		Super.BeginPlay();
		
		self.roll = random(1, 360);
		
		A_StartSound("Attack/PsychicBeam", CHAN_AUTO, 0, 0.25, ATTN_NORM);
	}
	
	override void tick()
	{
		Super.Tick();
		
		self.roll += 13;
	}
	
	States
	{
		Spawn:
			PSYC ABCDEFGHI 2 Bright;
			Loop;
		Death:
			#### # 1 A_FadeOut(0.075);
			Loop;
	}
}