Class GHAGWeapon : Weapon
{

	Default
	{
		+BloodSplatter;
	}

	mixin IcePunch;

	States
	{
		Select:
			TNT1 A 1 A_Raise();
			Loop;
		Deselect:
			TNT1 A 1 A_Lower();
			Loop;
		Fire:
			TNT1 A 1;
			TNT1 A 1 A_Refire();
			goto Ready;
		Ready:
			TNT1 A 1;
			stop;
	
		User1:
			#### A 0 A_IcePunchPrecalc();
		IcePunchSwing:
			#### A 1;
			#### A 0
			{
				if (Gardevoir(self).IcePunchTarget && distance3d(Gardevoir(self).IcePunchTarget) > 128 && invoker.IcePunchGetSpeed() > 16.0)
				{
					return resolveState("IcePunchSwing");
				}
				return resolveState(null);
			}
			#### A 0 A_StartSound("fists/ICEPUNCH", CHAN_AUTO);
			#### AAA 2 {
				A_OverlayIcePunch();
			}
			#### AA 2
			{
				A_OverlayIcePunch();
				A_IcePunch();
			}
			#### A 1
			{
				A_ClearOverlays(2, 2);
				Gardevoir(self).IcePunchTarget = null;
			}
			#### A 0 A_Jump(256, "Ready");
			stop;
		IcePunch.Overlay:
			IPUN AB 3 A_SetTics(Gardevoir(self).GetTicModifier(3));
			IPUN C 4;
			IPUN DEF 4 A_SetTics(Gardevoir(self).GetTicModifier(4));
			stop;
	}
}